apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'android-command'
apply plugin: 'com.neenbedankt.android-apt'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
    compile 'com.squareup.retrofit:converter-jackson:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    apt 'com.raizlabs.android:DBFlow-Compiler:2.2.1'
    compile "com.raizlabs.android:DBFlow:2.2.1"
    compile 'com.jakewharton.timber:timber:3.0.1'
    debugCompile 'com.facebook.stetho:stetho:1.1.1'
    compile project(':external:airmapview')
    compile 'com.google.maps.android:android-maps-utils:0.4'
    compile 'com.android.support:design:23.1.0'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.applidium.paris"
        versionCode 1
        versionName "0.1"
        minSdkVersion 14
        targetSdkVersion 23

        def Properties props = new Properties()
        props.load(new FileInputStream(new File('secrets.properties')))
        def googleMapsKey = "\"" + props['GOOGLE_MAPS_KEY'] + "\""
        resValue "string", "google_maps_key", googleMapsKey
    }

    productFlavors {
        stubs {
            applicationId "com.applidium.paris.stubs"
            buildConfigField "String", "ENDPOINT", "\"http://example.com\""
            versionName = android.defaultConfig.versionName + "-stubs"
        }

        preprod {
            applicationId "com.applidium.paris.preprod"
            versionName = android.defaultConfig.versionName + "-preprod"
        }

        prod {
            applicationId "com.applidium.paris.prod"
            versionName = android.defaultConfig.versionName + "-prod"
        }

        distrib {

        }
    }

    variantFilter { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.setIgnore(!variant.getFlavors().get(0).name.equals('distrib'));
        }
        if (variant.buildType.name.equals('debug')) {
            variant.setIgnore(variant.getFlavors().get(0).name.equals('distrib'));
        }
    }

    // Fix problem with jackson libs
    // https://code.google.com/p/android/issues/detail?id=61573
    packagingOptions {
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/LICENSE'
    }
}
