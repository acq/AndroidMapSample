apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'android-command'
apply plugin: 'com.neenbedankt.android-apt'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
    compile 'com.squareup.retrofit:converter-jackson:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    apt 'com.raizlabs.android:DBFlow-Compiler:2.2.1'
    compile "com.raizlabs.android:DBFlow:2.2.1"
    compile 'com.jakewharton.timber:timber:3.0.1'
    debugCompile 'com.facebook.stetho:stetho:1.1.1'
    compile project(':external:airmapview')
    compile 'com.android.support:design:23.1.0'
    compile 'com.google.android.gms:play-services-location:8.1.0'
    compile 'se.akerfeldt:okhttp-signpost:1.0.0'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.applidium.paris"
        versionCode 1
        versionName "0.1"
        minSdkVersion 14
        targetSdkVersion 23

        def Properties props = new Properties()
        props.load(new FileInputStream(new File('secrets.properties')))
        resValue "string", "google_maps_key", "\"" + props['GOOGLE_MAPS_KEY'] + "\""
        buildConfigField "String", "FOURSQUARE_CLIENT_ID", "\"" + props['FOURSQUARE_CLIENT_ID'] + "\""
        buildConfigField "String", "FOURSQUARE_CLIENT_SECRET", "\"" + props['FOURSQUARE_CLIENT_SECRET'] + "\""
        buildConfigField "String", "YELP_CONSUMER_KEY", "\"" + props['YELP_CONSUMER_KEY'] + "\""
        buildConfigField "String", "YELP_CONSUMER_SECRET", "\"" + props['YELP_CONSUMER_SECRET'] + "\""
        buildConfigField "String", "YELP_TOKEN", "\"" + props['YELP_TOKEN'] + "\""
        buildConfigField "String", "YELP_TOKEN_SECRET", "\"" + props['YELP_TOKEN_SECRET'] + "\""

        buildConfigField "String", "FOURSQUARE_ENDPOINT", "\"https://api.foursquare.com/v2\""
        buildConfigField "String", "YELP_ENDPOINT", "\"https://api.yelp.com/v2\""
    }

    productFlavors {
        stubs {
            applicationId "com.applidium.paris.stubs"
            versionName = android.defaultConfig.versionName + "-stubs"
        }

        preprod {
            applicationId "com.applidium.paris.preprod"
            versionName = android.defaultConfig.versionName + "-preprod"
        }

        prod {
            applicationId "com.applidium.paris.prod"
            versionName = android.defaultConfig.versionName + "-prod"
        }

        distrib {

        }
    }

    variantFilter { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.setIgnore(!variant.getFlavors().get(0).name.equals('distrib'));
        }
        if (variant.buildType.name.equals('debug')) {
            variant.setIgnore(variant.getFlavors().get(0).name.equals('distrib'));
        }
    }

    // Fix problem with jackson libs
    // https://code.google.com/p/android/issues/detail?id=61573
    packagingOptions {
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/LICENSE.txt'
    }
}
